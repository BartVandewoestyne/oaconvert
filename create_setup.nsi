; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Bart's Fantastic Airspace Maps"
!define PRODUCT_VERSION "1.00"
!define /date MyTIMESTAMP "%Y-%m-%d-%H-%M-%S"
!define PRODUCT_PUBLISHER "Bart Vandewoestyne"
!define PRODUCT_WEB_SITE "https://github.com/BartVandewoestyne/oaconvert"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "BartsFantasticAirspaceMaps-${PRODUCT_VERSION}-${MyTIMESTAMP}.exe"
LoadLanguageFile "${NSISDIR}\Contrib\Language files\Dutch.nlf"
LoadLanguageFile "${NSISDIR}\Contrib\Language files\English.nlf"
LoadLanguageFile "${NSISDIR}\Contrib\Language files\French.nlf"
InstallDir "$PROGRAMFILES\Airspace Maps"
Icon "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
UninstallIcon "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"
DirText "Setup will install $(^Name) in the following folder.$\r$\n$\r$\nTo install in a different folder, click Browse and select another folder."
LicenseText "If you accept all the terms of the agreement, choose I Agree to continue. You must accept the agreement to install $(^Name)."
LicenseData "creative_commons_license.txt"
LicenseForceSelection checkbox
ShowInstDetails show
ShowUnInstDetails show

; With 'user', the files are not copied... probably user has no access to this directory.
;RequestExecutionLevel user
RequestExecutionLevel admin

Page license
Page custom paypal
Page components
Page directory
Page instfiles

!macro writeRegistryEntries Name Fid
  WriteRegBin "HKLM" "Software\Garmin\MapSource\Families\${Name}"   "ID"   ${Fid}
  WriteRegStr "HKLM" "Software\Garmin\Mapsource\Families\${Name}"   "IDX"  "$INSTDIR\garmin\${Name}.mdx"
  WriteRegStr "HKLM" "Software\Garmin\Mapsource\Families\${Name}"   "TYP"  "$INSTDIR\garmin\${Name}.typ"
  WriteRegStr "HKLM" "Software\Garmin\Mapsource\Families\${Name}\1" "LOC"  "$INSTDIR\garmin"
  WriteRegStr "HKLM" "Software\Garmin\Mapsource\Families\${Name}\1" "BMAP" "$INSTDIR\garmin\${Name}.img"
  WriteRegStr "HKLM" "Software\Garmin\Mapsource\Families\${Name}\1" "TDB"  "$INSTDIR\garmin\${Name}.tdb"
  WriteRegStr "HKLM" "Software\Garmin\Mapsource\Families\${Name}\1" "MDR"  "$INSTDIR\garmin\${Name}.img"
!macroend

Function paypal
  MessageBox MB_OK "Disclaimer: we provide these maps as a free service.  Although we do our very best to make sure that the maps are accurate, we are aware that some might contain mistakes and we make no guarantee as to their absolute correctness.  These maps are provided “as is” in the hope that they will be useful, but WITHOUT ANY WARRANTY, to the extent permitted by law; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  Please use these maps as a guide and not as definitive information."
FunctionEnd

Function .onInit
#TODO: call UserInfo plugin to make sure user is admin
FunctionEnd

Section "License file" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "creative_commons_license.txt"
  SectionIn 1 RO
SectionEnd

Section "Garmin maps" SEC02
  SetOutPath "$INSTDIR\garmin"
  SetOverwrite ifnewer
  File /r "build\garmin\*.*"
SectionEnd

Section "GPX files" SEC03
  SetOutPath "$INSTDIR\gpx"
  SetOverwrite ifnewer
  File /r "build\gpx\*.*"
SectionEnd

Section "KML files" SEC04
  SetOutPath "$INSTDIR\kml"
  SetOverwrite ifnewer
  File /r "build\kml\*.*"
SectionEnd

Section "OpenAir files" SEC05
  SetOutPath "$INSTDIR\openair"
  SetOverwrite ifnewer
  File /r "build\openair\*.*"
  File /r "openair\belgium\*.*"
  File /r "openair\france\*.*"
  File /r "openair\germany\*.*"
  File /r "openair\luxembourg\*.*"
  File /r "openair\map_edges.txt"
SectionEnd

Section "Polish Format files" SEC06
  SetOutPath "$INSTDIR\polish_format"
  SetOverwrite ifnewer
  File /r "build\polish_format\*.*"
SectionEnd

Section -AdditionalIcons
  SetShellVarContext all
  CreateDirectory "$SMPROGRAMS\Airspace Maps"
  CreateShortCut "$SMPROGRAMS\Airspace Maps\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post

  SetShellVarContext all
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"

  !include build\nsis\create_registry_keys.nsi
  
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall

  SetShellVarContext all

  Delete "$INSTDIR\uninst.exe"
  
  Delete "$INSTDIR\polish_format\*.*"
  Delete "$INSTDIR\garmin\*.*"
  Delete "$INSTDIR\gpx\*.*"
  Delete "$INSTDIR\kml\*.*"
  Delete "$INSTDIR\openair\*.*"
  Delete "$INSTDIR\creative_commons_license.txt"

  RMDir "$INSTDIR\polish_format"
  RMDir "$INSTDIR\garmin"
  RMDir "$INSTDIR\gpx"
  RMDir "$INSTDIR\kml"
  RMDir "$INSTDIR\openair"
  RMDir "$INSTDIR"

  Delete "$SMPROGRAMS\Airspace Maps\Uninstall.lnk"
  RMDir "$SMPROGRAMS\Airspace Maps"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  
  !include build\nsis\delete_registry_keys.nsi
  
  ;SetAutoClose true
SectionEnd
